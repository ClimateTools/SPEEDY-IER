
      SUBROUTINE VDIFSC (UA,VA,SE,RH,QA,QSAT,PHI,ICNV,
     &                   UTENVD,VTENVD,TTENVD,QTENVD)
C--
C--   SUBROUTINE VDIFSC (UA,VA,SE,RH,QA,QSAT,PHI,ICNV,
C--  &                   UTENVD,VTENVD,TTENVD,QTENVD)
C--
C--   Purpose: Compute tendencies of momentum, energy and moisture
C--            due to vertical diffusion and shallow convection
C--   Input:   UA     = u-wind                           (3-dim)
C--            VA     = v-wind                           (3-dim)
C--            SE     = dry static energy                (3-dim)
C--            RH     = relative humidity [0-1]          (3-dim)
C--            QA     = specific humidity [g/kg]         (3-dim)
C--            QSAT   = saturation sp. humidity [g/kg]   (3-dim)
C--            PHI    = geopotential                     (3-dim)
C--            ICNV   = index of deep convection         (2-dim)
C--   Output:  UTENVD = u-wind tendency                  (3-dim)
C--            VTENVD = v-wind tendency                  (3-dim)
C--            TTENVD = temperature tendency             (3-dim)
C--            QTENVD = sp. humidity tendency [g/(kg s)] (3-dim)
C--
C     Resolution parameters

      include "atparam.h"
      include "atparam1.h"

      PARAMETER ( NLON=IX, NLAT=IL, NLEV=KX, NGP=NLON*NLAT )

C     Physical constants + functions of sigma and latitude

      include "com_physcon.h"

C     Vertical diffusion constants

      include "com_vdicon.h"

C     Water isotope tracer constants (qtiny)

      include "com_isocon.h"

      REAL UA(NGP,NLEV), VA(NGP,NLEV), SE(NGP,NLEV),
     &     RH(NGP,NLEV), QA(NGP,NLEV,NTR), QSAT(NGP,NLEV),
     &     PHI(NGP,NLEV)

      INTEGER ICNV(NGP)

      REAL UTENVD(NGP,NLEV), VTENVD(NGP,NLEV),
     &     TTENVD(NGP,NLEV), QTENVD(NGP,NLEV,NTR)

      REAL RSIG(NLEV), RSIG1(NLEV)

      REAL TRRAT(NGP,NLEV,NTR)


C--   1. Initalization

C     N.B. In this routine, fluxes of dry static energy and humidity
C          are scaled in such a way that:
C          d_T/dt = d_F'(SE)/d_sigma,  d_Q/dt = d_F'(Q)/d_sigma

      NL1  = NLEV-1
      CSHC = DSIG(NLEV)/3600.
      CVDI = (SIGH(NL1)-SIGH(1))/((NL1-1)*3600.)

      FSHCQ  = CSHC/TRSHC
      FSHCSE = CSHC/(TRSHC*CP)

      FVDIQ  = CVDI/TRVDI
      FVDISE = CVDI/(TRVDS*CP)

      DO K=1,NL1
        RSIG(K)=1./DSIG(K)
	RSIG1(K)=1./(1.-SIGH(K))
      ENDDO
      RSIG(NLEV)=1./DSIG(NLEV)
   
      DO K=1,NLEV
        DO J=1,NGP
          UTENVD(J,K) = 0.
          VTENVD(J,K) = 0.
          TTENVD(J,K) = 0.
        ENDDO
      ENDDO
      DO ITR=1,NTR
        DO K=1,NLEV
          DO J=1,NGP
            QTENVD(J,K,ITR) = 0.
            if (QA(J,K,IXH2O) .lt. qtiny) then
              TRRAT(J,K,ITR) = 0.
            ELSE
              TRRAT(J,K,ITR) = QA(J,K,ITR)/QA(J,K,IXH2O)
            ENDIF
          ENDDO
        ENDDO
      ENDDO


C--   2. Shallow convection

      DRH0   = RHGRAD*(SIG(NLEV)-SIG(NL1))
      FVDIQ2 = FVDIQ*SIGH(NL1)

      DO J=1,NGP

        DMSE = (SE(J,NLEV)-SE(J,NL1))+
     &             ALHC*(QA(J,NLEV,1)-QSAT(J,NL1))
        DRH  = RH(J,NLEV)-RH(J,NL1)
        FCNV = 1.

        IF (DMSE.GE.0.0) THEN

          IF (ICNV(J).GT.0) FCNV = REDSHC

          FLUXSE         = FCNV*FSHCSE*DMSE
          TTENVD(J,NL1)  = FLUXSE*RSIG(NL1)
          TTENVD(J,NLEV) =-FLUXSE*RSIG(NLEV)

          IF (DRH.GE.0.0) THEN
            ffac = FCNV*FSHCQ*QSAT(J,NLEV)
            FLUXQ            = ffac*DRH
            QTENVD(J,NL1 ,1) = FLUXQ*RSIG(NL1) 
            QTENVD(J,NLEV,1) =-FLUXQ*RSIG(NLEV)
            DO ITR=2,NTR
              FUP = ffac*RH(J,NLEV)*TRRAT(J,NLEV,ITR)
              FDN = ffac*RH(J,NL1 )*TRRAT(J,NL1 ,ITR)
              FLUXTR = FUP-FDN
              QTENVD(J,NL1 ,ITR) =  FLUXTR*RSIG(NL1)
              QTENVD(J,NLEV,ITR) = -FLUXTR*RSIG(NLEV)
            ENDDO
          ENDIF


        ELSE IF (DRH.GE.DRH0) THEN

          ffac = FVDIQ2*QSAT(J,NL1)
          FLUXQ            = ffac*DRH
          QTENVD(J,NL1 ,1) = FLUXQ*RSIG(NL1) 
          QTENVD(J,NLEV,1) =-FLUXQ*RSIG(NLEV)
          DO ITR=2,NTR
            FUP = ffac*RH(J,NLEV)*TRRAT(J,NLEV,ITR)
            FDN = ffac*RH(J,NL1 )*TRRAT(J,NL1 ,ITR)
            FLUXTR = FUP-FDN
            QTENVD(J,NL1 ,ITR) =  FLUXTR*RSIG(NL1)
            QTENVD(J,NLEV,ITR) = -FLUXTR*RSIG(NLEV)
          ENDDO

        ENDIF

      ENDDO

C--   3. Vertical diffusion of moisture above the PBL

      DO K=3,NLEV-2

        IF (SIGH(K).GT.0.5) THEN

          DRH0   = RHGRAD*(SIG(K+1)-SIG(K))
          FVDIQ2 = FVDIQ*SIGH(K)

          DO J=1,NGP

            DRH=RH(J,K+1)-RH(J,K)

            IF (DRH.GE.DRH0) THEN
              FLUXQ          = FVDIQ2*QSAT(J,K)*DRH
              QTENVD(J,K  ,1)= QTENVD(J,K  ,1)+FLUXQ*RSIG(K)
              QTENVD(J,K+1,1)= QTENVD(J,K+1,1)-FLUXQ*RSIG(K+1)
              DO ITR=2,NTR
                FUP = FVDIQ2*QSAT(J,K)*RH(J,K+1)*TRRAT(J,K+1,ITR)
                FDN = FVDIQ2*QSAT(J,K)*RH(J,K  )*TRRAT(J,K  ,ITR)
                FLUXTR = FUP-FDN
                QTENVD(J,K  ,ITR) = 
     &                      QTENVD(J,K  ,ITR)+FLUXTR*RSIG(K)
                QTENVD(J,K+1,ITR) = 
     &                      QTENVD(J,K+1,ITR)-FLUXTR*RSIG(K+1)
              ENDDO
            ENDIF

          ENDDO

        ENDIF

      ENDDO

C--   4. Damping of super-adiabatic lapse rate

      DO K=1,NL1
       DO J=1,NGP

         SE0 = SE(J,K+1)+SEGRAD*(PHI(J,K)-PHI(J,K+1))

         IF (SE(J,K).LT.SE0) THEN
           FLUXSE      = FVDISE*(SE0-SE(J,K))
           TTENVD(J,K) = TTENVD(J,K)+FLUXSE*RSIG(K)
           DO K1=K+1,NLEV
             TTENVD(J,K1) = TTENVD(J,K1)-FLUXSE*RSIG1(K)
           ENDDO
         ENDIF

       ENDDO
      ENDDO

C--

      RETURN
      END
